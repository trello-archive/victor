/*
 * Copyright 2015 Trello, Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.13.0"
    id 'maven-publish'
    id 'groovy'
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

// This makes it easier to figure out issues with Travis CI builds
test {
    testLogging.showStandardStreams = true
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Batik dependencies

dependencies {
    implementation 'org.apache.xmlgraphics:batik-codec:1.12'
    implementation 'org.apache.xmlgraphics:batik-anim:1.12'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.4'
    implementation 'com.romainpiel.svgtoandroid:svgtoandroid:0.1.0'
    testImplementation 'junit:junit:4.13.2'
}

// Plugin publishing

group = 'com.trello'
version = '1.1.1-SNAPSHOT'


gradlePlugin {
    plugins {
        victorPlugin {
            id = 'com.trello.victor'
            implementationClass = 'com.trello.victor.VictorPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/trello/victor'
    vcsUrl = 'https://github.com/trello/victor'
    description = 'Gradle plugin that lets an Android project use SVGs as resources.'
    tags = ['gradle', 'plugin', 'android']

    plugins {
        victorPlugin {
            displayName = 'victor'
        }
    }

    mavenCoordinates {
        groupId = "com.trello"
        artifactId = "victor"
    }
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = '../local-plugin-repository'
        }
    }
}